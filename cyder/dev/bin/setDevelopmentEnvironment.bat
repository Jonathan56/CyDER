@echo off

rem ------------------------------------------------------------------
rem Batch file that sets environment variables for FMU.
rem This file sets environment variables for the FMU.
rem It is only needed for users who develop and compile the
rem FMU. 
rem This file gets all environment variables from
rem the file systemVariables-windows.properties
rem
rem The file need to be executed from the binary directory of the 
rem FMU (fmu\bin), otherwise an error message will be written.
rem
rem The file is based on the similar file used for BCVTB written by 
rem Michael Wetter.
rem
rem Wangda Zuo
rem WZuo@lbl.gov                                         2012-04-27
rem ------------------------------------------------------------------

rem ------------------------------------------------------------------
rem    Read environment variables from systemVariables-windows.properties
rem ------------------------------------------------------------------
rem Flag, set to true on 32 bit systems, or to false on 64 bit systems
@set FMU_JAR=%~dp0\FMU.jar
for /F "delims=" %%i in ('java -jar "%FMU_JAR%" --getEnvironmentVariable FMU_32bit') do set FMU_32bit=%%i
rem Batch file that sets environment variables for Visual Studio C compiler
for /F "delims=" %%i in ('java -jar "%FMU_JAR%" --getEnvironmentVariable FMU_VS_BAT') do set FMU_VS_BAT="%%i"
rem Path variable so that dll's can be found. Don't quote Path as it contain
rem many entries.
for /F "delims=" %%i in ('java -jar "%FMU_JAR%" --getEnvironmentVariable Path') do set Path=%%i

:SETVARS1
cd ..
@set FMU_HOME=%CD%
cd bin
@set ANT_HOME=%FMU_HOME%\lib\apache-ant

IF %FMU_32bit%==true  echo Configuring FMU development environment for 32 bit.
IF %FMU_32bit%==false echo Configuring FMU development environment for 64 bit.

:CHECKVC
rem --------------------------------------------------
rem ------------- C++ compiler -----------------------
rem --------------------------------------------------
rem The next command sets VCINSTALLDIR if available
if defined FMU_VS_BAT call %FMU_VS_BAT%
if exist "%VCINSTALLDIR%\vcvarsall.bat" (
  IF %FMU_32bit%==true  call "%VCINSTALLDIR%\vcvarsall.bat"
  IF %FMU_32bit%==false call "%VCINSTALLDIR%\vcvarsall.bat" amd64
  goto SETVARS2
)
echo **************************************************************
echo *** Warning: Did not find Visual C++ compiler.
echo  Environment variable FMU_VCDIR does not point to Visual C++ directory.
echo  You will not be able to compile source code.
echo  You will be able to use compiled code.
echo  C++ batch file is set to 
echo  %FMU_VS_BAT%.
echo  If C++ is installed, adjust the file 
echo  %FMU_HOME%\bin\systemVariables-windows.properties

:SETVARS2
@set Path=%Path%;%ANT_HOME%\bin
@set CLASSPATH=%PTII%;%CLASSPATH%;%FMU_HOME%\lib\cpptasks.jar
@set FMU_OS=windows 

:WRITECONFIG
rem --------------------------------------------------
rem -----Generate configuration file for ant ---------
rem --------------------------------------------------
cd %FMU_HOME%
echo // This file was autogenerated on %date% > build.properties
echo // Changes to this file will be lost whenever >> build.properties
echo // %0 is executed.  >> build.properties

:END
